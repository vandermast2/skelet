apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.samapps.skelet"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

        vectorDrawables.useSupportLibrary = true
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
        check 'Interoperability'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            dimension "default"
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "skelet.DEV"
            buildConfigField 'String', 'API_BASE_URL', '"http://217.71.92.66/jpa/cv.dev/"'
            buildConfigField 'String', 'SOCKET_BASE_URL', '"tcp://217.71.92.68:443"'
            buildConfigField 'String', 'TOKEN', '"in342yxro7yn3o8ycrf98pq3yxp9r8uq38u4rtpt0q38up480f3fb8347hf374hf037h40f3h4f037h4f03h84f0h83450f8j0j58fjh"'
            buildConfigField 'String', 'TOKEN1', '"in342yxro7yn3o8y"'
        }
        devTest {
            dimension "default"
            resValue "string", "app_name", "skelet.Test"
            applicationIdSuffix ".devTest"
            buildConfigField 'String', 'API_BASE_URL', '"http://"'
            buildConfigField 'String', 'SOCKET_BASE_URL', '"tcp://"'
            buildConfigField 'String', 'TOKEN', '""'
            buildConfigField 'String', 'TOKEN1', '""'
        }
        prodTest {
            dimension "default"
            resValue "string", "app_name", "skelet.ProdTest"
            applicationIdSuffix ".prodTest"
            buildConfigField 'String', 'API_BASE_URL', '"https://"'
            buildConfigField 'String', 'SOCKET_BASE_URL', '"tcp://"'
            buildConfigField 'String', 'TOKEN', '""'
            buildConfigField 'String', 'TOKEN1', '""'
        }
        prod {
            dimension "default"
            applicationIdSuffix ".prod"
            resValue "string", "app_name", "skelet"
            buildConfigField 'String', 'API_BASE_URL', '""'
            buildConfigField 'String', 'SOCKET_BASE_URL', '""'
            buildConfigField 'String', 'TOKEN', '""'
            buildConfigField 'String', 'TOKEN1', '""'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {

                def SEP = "_"
                def flavor = variant.productFlavors[0].name
                def buildType = variant.variantData.variantConfiguration.buildType.name
                def version = variant.versionName
                def versionCode = variant.versionCode
                def date = new Date()
                def formattedDate = date.format('dd-MM-yyyy-HH_mm')

                outputFileName = "${flavor}${SEP}${buildType}${SEP}${version}${SEP}${versionCode}${SEP}${formattedDate}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0-beta01'

    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.anko:anko-coroutines:$anko_version"

    def stetho_ver = '1.5.0'
    implementation "com.facebook.stetho:stetho:$stetho_ver"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_ver"

    implementation 'com.facebook.fresco:fresco:1.7.1'

    implementation 'com.jakewharton.timber:timber:4.7.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex:rxkotlin:0.60.0'

    def retrofit_version = '2.4.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-experimental-adapter:1.0.0'

    def ok_http_version = '3.8.1'
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_http_version"

    def dagger_version = '2.16'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation 'com.google.firebase:firebase-messaging:17.1.0'

    implementation "org.jetbrains.anko:anko:$anko_version"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true;
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    implementation 'com.google.android.material:material:1.0.0-beta01'
}
kotlin {
    experimental {
        coroutines "enable"
    }
}
